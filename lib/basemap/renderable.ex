defmodule Basemap.Renderable do
  @doc """
  Returns the project-relative path to the artifact generated by this layer. For
  intermediary layers that's usally a .mbtiles file in the cache directory.
  Final layers are usually a directory with individual .pbf files, ready to be
  served.
  """
  @callback target(:container | :cache) :: Path.t()

  @doc """
  Should return true if the tiles are outdated.
  """
  @callback stale?() :: boolean()

  @doc """
  Render/create the tiles.
  """
  @callback render() :: :ok | {:error, binary()}

  defmacro __using__(_opts) do
    quote do
      require Benchmark
      @behaviour Basemap.Renderable

      def name(), do: Path.basename(unquote(__CALLER__.file), ".ex")
      def path(:cache, extra), do: Path.join("data/cache/basemap/#{name()}", extra)
      def path(:container, extra), do: Path.join("/workdir/basemap/#{name()}", extra)

      def ensure() do
        Benchmark.measure("rendering basemap #{name()}", fn ->
          if stale?(), do: render(), else: :ok
        end)
      end

      @impl Basemap.Renderable
      def target(where), do: path(where, "")
      defoverridable target: 1
    end
  end
end
